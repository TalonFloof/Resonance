plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

repositories {
    maven {
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    /*parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }*/
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    implementation "me.fzzyhmstrs:fzzy_config:${project.fzzy_version}+${project.fzzy_mc_version}+neoforge"
    compileOnly "maven.modrinth:serene-seasons:${project.serene_seasons_version}-neoforge,${project.glitch_mc_version}"
    compileOnly 'com.github.glitchfiend:GlitchCore-neoforge:' + glitch_mc_version + '-' + glitchcore_version
}

sourceSets.main.resources { srcDir 'src/generated/resources' }